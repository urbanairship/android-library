version = airshipProperties.version
group = "com.urbanairship.android"
description = "Urban Airship Android SDK"

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// For test coverage reports
apply plugin: 'jacoco'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    // Android M removed Apache HTTP Client, so this is needed for unit tests
    useLibrary 'org.apache.http.legacy'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23

        buildConfigField "String", "URBAN_AIRSHIP_VERSION", "\"${project.version}\""

        consumerProguardFiles 'consumer-proguard.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

configurations {
    doclava
}

dependencies {
    // Required
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:support-annotations:23.3.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'

    // Optional
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'

    provided files('libs/amazon-device-messaging-1.0.1.jar')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'

    doclava("com.google.doclava:doclava:1.0.6")
}

android.libraryVariants.all { variant ->

    // Add a javadoc task for each variant
    task("javadoc${variant.name.capitalize()}", type: Javadoc, dependsOn: "assemble${variant.name.capitalize()}") {
        title = null
        source = variant.javaCompile.source

        // The classpaths are not fully available until after assemble task
        doFirst {
            classpath += variant.javaCompile.classpath
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        }

        exclude '**/BuildConfig.java'
        exclude '**/R.java'

        options {
            docletpath = configurations.doclava.files.asType(List)
            doclet "com.google.doclava.Doclava"
            bootClasspath new File(System.getenv('JAVA_HOME') + "/jre/lib/rt.jar")
            addStringOption "hdf project.name", "Urban Airship Android Library"
            addStringOption "hdf project.version", project.version
            addStringOption "apiversion", "v2"
            addStringOption "federate android", "http://developer.android.com/reference"
            addStringOption "federationxml android", "docs/android-22.xml"
            addStringOption "templatedir", "docs/template"
        }
    }


}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId project.group
                artifactId 'urbanairship-sdk'

                // Add your description here
                name project.name
                description project.description
                url 'http://www.urbanairship.com'
            }

            whenConfigured { MavenPom pom ->
                pom.dependencies.each {
                    if (it.artifactId == 'play-services-location' || it.artifactId == 'cardview-v7' || it.artifactId == 'appcompat-v7') {
                        it.optional = true
                    }
                }
            }
        }
    }
}

bintray {
    if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayApiKey')) {
        // Credentials should be stored in ~/.gradle/gradle.properties
        user = bintrayUser
        key =  bintrayApiKey
    }

    configurations = ['archives']

    pkg {
        userOrg = 'urbanairship'
        name = project.name
        desc = project.description
        websiteUrl = 'http://www.urbanairship.com'

        version {
            desc = project.description
            name = project.version
            released = new Date()
        }
    }
}

task bintrayUploadRelease {
    doFirst {
        bintrayUpload.repoName = 'android'
        bintrayUpload.publish = false
    }
    finalizedBy 'bintrayUpload'
}

task bintrayUploadInternal {
    doFirst {
        bintrayUpload.repoName = 'android-internal'
        bintrayUpload.publish = true
    }
    finalizedBy 'bintrayUpload'
}


//generates NativeBridge.java
task templatizeNativeBridge(type: Exec) {
    commandLine 'src/main/js/templatize'
}

task javadoc(dependsOn: 'javadocRelease')

def getDexMethodCount() {
    try {
        def dx = "${android.getSdkDirectory()}/build-tools/${android.getBuildToolsRevision()}/dx"
        def jar = "${project.buildDir}/intermediates/bundles/release/classes.jar"
        def output = new ByteArrayOutputStream()
        exec {
            commandLine "../tools/dex-method-count.sh", dx, jar
            standardOutput = output
        }
        return output.toString().trim()
    } catch (e) {
        println(e)
        return ""
    }
}

task runDexMethodCount << {
    def count = getDexMethodCount()

    File dex = file("${project.buildDir}/dex-counts/release")
    dex.getParentFile().mkdirs()
    dex.text = "Dex Method Count: ${count}"
    println("Dex Method Count: ${count}")
}

project.afterEvaluate {
    prepareDebugDependencies.dependsOn(templatizeNativeBridge)
    prepareReleaseDependencies.dependsOn(templatizeNativeBridge)
    runDexMethodCount.dependsOn(assembleRelease)
    bintrayUpload.dependsOn(assembleRelease)
}
