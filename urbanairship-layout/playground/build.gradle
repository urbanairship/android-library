import com.fasterxml.jackson.databind.ObjectMapper
import com.fasterxml.jackson.dataformat.yaml.YAMLFactory

buildscript {
    ext {
        jacksonVersion='2.9.8'
    }

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'maven-publish'
}

group = "com.urbanairship.android"

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.urbanairship.android.layout.playground"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            assets.srcDirs += file("$buildDir/generated/sample_layout_assets/")
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    namespace 'com.urbanairship.android.layout.playground'
}

dependencies {
    implementation project(":urbanairship-layout")
    implementation project(":urbanairship-automation")

    implementation "androidx.core:core-ktx:${rootProject.androidxCoreVersion}"
    implementation "androidx.appcompat:appcompat:${rootProject.androidxAppCompatVersion}"
    implementation "com.google.android.material:material:${rootProject.materialVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${rootProject.androidxConstraintLayoutVersion}"

    // Leak Canary (for debug builds only)
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'

    testImplementation "junit:junit:${rootProject.junitVersion}"

    androidTestImplementation "androidx.test:core-ktx:${rootProject.androidxTestCoreVersion}"
    androidTestImplementation "androidx.test.ext:junit-ktx:${rootProject.androidxTestJunitVersion}"
    androidTestImplementation "androidx.test:runner:${rootProject.androidxTestRunnerVersion}"
    androidTestImplementation "androidx.test:rules:${rootProject.androidxTestRulesVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${rootProject.androidxEspressoVersion}"
}

task generateSampleLayouts {
    description = 'Converts yml sample layouts to json files to be merged into assets.'

    def inFiles = fileTree(dir: 'sample-layouts', include: ['*.yml', '*.yaml'])
    def outDir = file("$buildDir/generated/sample_layout_assets/sample_layouts")

    inputs.files(inFiles.toList()).withPropertyName('sourceFiles')
    outputs.dir(outDir).withPropertyName('outputDir')
    outputs.cacheIf {true }

    def yamlReader = new ObjectMapper(new YAMLFactory())
    def jsonWriter = new ObjectMapper()

    doLast {
        if (outDir.exists()) {
            outDir.delete()
        }
        outDir.mkdirs()

        inFiles.each { input ->
            def name = input.getName().replaceFirst(~/ya?ml/, "json")
            def outFile = file("$outDir/$name")
            outFile.createNewFile()

            def yaml = yamlReader.readValue(input, Object.class)
            jsonWriter.writeValue(outFile, yaml)
        }
    }
}

android.applicationVariants.all { variant ->
    variant.getMergeAssetsProvider().configure {
        dependsOn generateSampleLayouts
    }
}