apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion

    lintOptions {
        disable 'MissingTranslation', 'AppCompatResource'
        abortOnError false
    }

    defaultConfig {
        applicationId "com.urbanairship.sample"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$rootProject.androidxAppCompatVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "androidx.preference:preference:$rootProject.androidxPreferenceVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.androidxLifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$rootProject.androidxLifeCycleVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.androidxConstraintLayoutVersion"
    implementation "androidx.navigation:navigation-fragment:$rootProject.androidxNavigationVersion"
    implementation "androidx.navigation:navigation-ui:$rootProject.androidxNavigationVersion"

    // Airship ADM
    implementation project(':urbanairship-adm')

    // Airship FCM
    implementation project(':urbanairship-fcm')

    // Airship Debug: Normally this is a debug only dependency but we
    // embed the view in sample app
    implementation project(':urbanairship-debug')

    // Airship Advertising ID tracking
    implementation project(':urbanairship-ads-identifier')

    // Airship Preferences
    implementation project(':urbanairship-preference')

    // Airship Message Center
    implementation project(':urbanairship-messagecenter')

    // Airship Automation (In-App)
    implementation project(':urbanairship-automation')

    // Airship location
    implementation project(':urbanairship-location')
    // Allows Airship location services to use Fused Location
    implementation "com.google.android.gms:play-services-location:$rootProject.playServicesLocationVersion"

    // Testing-only dependencies
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.androidxEspressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.androidxEspressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$rootProject.androidxEspressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.androidxEspressoVersion"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$rootProject.androidxEspressoVersion"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation "androidx.test:runner:$rootProject.androidxTestRunnerVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.androidxTestRulesVersion"
    androidTestImplementation "androidx.test:core:$rootProject.androidxTestCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.androidxTestJunitVersion"

    // Assertions
    androidTestImplementation "androidx.test.ext:truth:$rootProject.androidxTestTruthVersion"
    androidTestImplementation "com.google.truth:truth:$rootProject.googleTruthVersion"
}


def googleServicesFile = rootProject.file('sample/google-services.json')
if (googleServicesFile.exists()) {
    apply plugin: 'com.google.gms.google-services'  // Google Play services Gradle plugin
}
