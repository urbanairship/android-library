buildscript {

    ext {
        // Airship Version - major.minor.patch
        airshipVersion = '16.1.1'

        // Airship Version Qualifier beta, release, etc...
        // airshipVersionQualifier = "beta"

        // Android SDK Versions
        compileSdkVersion = 31
        targetSdkVersion = 31
        minSdkVersion = 21

        // Gradle plugins
        gradlePluginVersion = '4.1.3'
        bintrayPluginVersion = '1.8.4'
        googleServicesPluginVersion = '4.3.10'
        spotlessPluginVersion = '5.17.0'

        // Spotless plugins
        ktlintVersion = '0.41.0'

        // Dependencies

        // Kotlin
        kotlinVersion = '1.4.32'
        kotlinCoroutinesVersion = '1.4.3'

        // Androidx
        androidxCoreVersion = '1.6.0'
        androidxFragmentVersion = '1.3.6'
        androidxAnnotationVersion = '1.2.0'
        androidxCustomViewVersion = '1.1.0'
        androidxAppCompatVersion = '1.3.1'
        androidxPreferenceVersion = '1.1.1'
        androidxVectorDrawableVersion = '1.1.0'
        androidxRoomVersion = '2.3.0'
        androidxConstraintLayoutVersion = '2.1.1'
        androidxPagingVersion = '2.1.2'
        androidxLifeCycleVersion = '2.3.1'
        androidxNavigationVersion = '2.3.5'
        androidxSwipeRefreshLayoutVersion = '1.1.0'
        androidXWork = '2.7.0'
        androidXConcurrentFutures = '1.1.0'

        // Material design
        materialVersion = '1.4.0'

        // Google Play Services
        playServicesAdsIdentifierVersion = '17.1.0'
        playServicesLocationVersion = '18.0.0'
        playServicesBaseVersion = '17.6.0'

        // Firebase
        firebaseMessagingVersion = '22.0.0'

        // OkHttp (for WebSocket support in airship-chat)
        okHttpVersion = '4.9.2'

        // Test
        androidxTestTruthVersion = '1.2.0'
        androidxTestRunnerVersion = '1.2.0'
        androidxTestRulesVersion = '1.2.0'
        androidxTestCoreVersion = '1.3.0'
        androidxTestJunitVersion = '1.1.2'
        androidxEspressoVersion = '3.2.0'
        kotlinxCorutinesTest = '1.4.3'
        googleTruthVersion = '1.0.1'
        junitVersion = '4.13.2'
        mockitoVersion = '3.9.0'
        mockitoKotlinVersion = '3.1.0'
        robolectricVersion = '4.3.1'

        // Doc Dependencies
        doclavaVersion = '1.0.6'
        dokkaVersion = '1.4.32'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "com.google.gms:google-services:$googleServicesPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    }
}

plugins {
    id 'airship-doclava'
    id 'airship-dokka'
    id 'airship-publish'
    id 'com.diffplug.spotless' version "$spotlessPluginVersion"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task packageDocs() {
    dependsOn(packageJavadoc, packageKDoc)
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Generate Javadoc and KDoc archives for release"
}

task getVersion() {
    doLast {
        def version = airshipVersion
        if (project.hasProperty("airshipVersionQualifier")) {
            version += "-" + airshipVersionQualifier
        }
        println version
    }
}

spotless {
    java {
        target '**/*.java'
        targetExclude '.idea/**'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    def ktlintUserData = [
        'android': 'true',
        'max_line_length': 'off',
        // Match Android Kotlin style guide import order.
        // https://developer.android.com/kotlin/style-guide
        'kotlin_imports_layout': 'ascii',
        // Disable 'indent' to avoid overly aggressive wrapping of function parameters.
        'disabled_rules': 'indent'
    ]
    kotlin {
        target '**/*.kt'
        targetExclude '.idea/**', 'buildSrc/**'

        ktlint(ktlintVersion).userData(ktlintUserData)

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    kotlinGradle {
        target 'buildSrc/**/*.kts', 'buildSrc/**/*.kt'
        targetExclude 'buildSrc/build/**'

        ktlint(ktlintVersion).userData(ktlintUserData)

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}
