buildscript {

    ext {
        // Airship Version - major.minor.patch
        airshipVersion = '16.1.0'

        // Airship Version Qualifier beta, release, etc...
        // airshipVersionQualifier = "beta"

        // Android SDK Versions
        compileSdkVersion = 31
        targetSdkVersion = 31
        minSdkVersion = 21

        // Gradle plugins
        gradlePluginVersion = '7.0.3'
        googleServicesPluginVersion = '4.3.10'
        spotlessPluginVersion = '5.17.0'

        // Spotless plugins
        ktlintVersion = '0.41.0'

        // Dependencies

        // Kotlin
        kotlinVersion = '1.5.31'
        kotlinCoroutinesVersion = '1.5.2'

        // Androidx
        androidxCoreVersion = '1.7.0'
        androidxFragmentVersion = '1.4.0'
        androidxAnnotationVersion = '1.3.0'
        androidxCustomViewVersion = '1.1.0'
        androidxAppCompatVersion = '1.4.0'
        androidxPreferenceVersion = '1.1.1'
        androidxVectorDrawableVersion = '1.1.0'
        androidxRoomVersion = '2.3.0'
        androidxConstraintLayoutVersion = '2.1.2'
        androidxPagingVersion = '2.1.2'
        androidxLifeCycleVersion = '2.4.0'
        androidxNavigationVersion = '2.3.5'
        androidxSwipeRefreshLayoutVersion = '1.1.0'
        androidXWork = '2.7.1'
        androidXConcurrentFutures = '1.1.0'

        // Material design
        materialVersion = '1.4.0'

        // Google Play Services
        playServicesAdsIdentifierVersion = '17.1.0'
        playServicesLocationVersion = '18.0.0'
        playServicesBaseVersion = '17.6.0'

        // Firebase
        firebaseMessagingVersion = '23.0.0'

        // OkHttp (for WebSocket support in airship-chat)
        okHttpVersion = '4.9.3'

        // Test
        androidxTestTruthVersion = '1.4.0'
        androidxTestRunnerVersion = '1.4.0'
        androidxTestRulesVersion = '1.4.0'
        androidxTestCoreVersion = '1.4.0'
        androidxTestJunitVersion = '1.1.3'
        androidxEspressoVersion = '3.4.0'
        kotlinxCorutinesTest = '1.5.2'
        googleTruthVersion = '1.1.3'
        junitVersion = '4.13.2'
        mockitoVersion = '4.1.0'
        mockitoKotlinVersion = '4.0.0'
        robolectricVersion = '4.7.3'

        // Doc Dependencies
        doclavaVersion = '1.0.6'
        dokkaVersion = '1.4.32'
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "com.google.gms:google-services:$googleServicesPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    }
}

plugins {
    id 'airship-doclava'
    id 'airship-dokka'
    id 'airship-publish'
    id 'com.diffplug.spotless' version "$spotlessPluginVersion"
}

task packageDocs() {
    dependsOn(packageJavadoc, packageKDoc)
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Generate Javadoc and KDoc archives for release"
}

task getVersion() {
    doLast {
        def version = airshipVersion
        if (project.hasProperty("airshipVersionQualifier")) {
            version += "-" + airshipVersionQualifier
        }
        println version
    }
}

spotless {
    java {
        target '**/*.java'
        targetExclude '.idea/**'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    def ktlintUserData = [
        'android': 'true',
        'max_line_length': 'off',
        // Match Android Kotlin style guide import order.
        // https://developer.android.com/kotlin/style-guide
        'kotlin_imports_layout': 'ascii',
        // Disable 'indent' to avoid overly aggressive wrapping of function parameters.
        'disabled_rules': 'indent'
    ]
    kotlin {
        target '**/*.kt'
        targetExclude '.idea/**', 'buildSrc/**'

        ktlint(ktlintVersion).userData(ktlintUserData)

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    kotlinGradle {
        target 'buildSrc/**/*.kts', 'buildSrc/**/*.kt'
        targetExclude 'buildSrc/build/**'

        ktlint(ktlintVersion).userData(ktlintUserData)

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}
