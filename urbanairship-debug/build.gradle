plugins {
    id 'airship-module'
    id 'kotlin-android'
}

description = "Airship Android Debug Library"

airshipModule {
    published = false
}

android {
    buildFeatures{
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    
    namespace 'com.urbanairship.debug'
}

dependencies {
    api project(':urbanairship-core')
    implementation project(':urbanairship-ads-identifier')
    implementation project(':urbanairship-automation')
    implementation project(':urbanairship-location')
    implementation project(':urbanairship-message-center')
    implementation project(':urbanairship-preference-center')
    implementation project(':urbanairship-preference')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.kotlinCoroutinesVersion"

    // Room
    implementation "androidx.room:room-runtime:$rootProject.androidxRoomVersion"
//    kapt "androidx.room:room-compiler:$rootProject.androidxRoomVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.androidxNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.androidxNavigationVersion"

    // Constraint layout
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.androidxConstraintLayoutVersion"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-common-java8:$rootProject.androidxLifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.androidxLifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.androidxLifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$rootProject.androidxLifeCycleVersion"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$rootProject.androidxPagingVersion"

    // Support libraries
    implementation "androidx.appcompat:appcompat:$rootProject.androidxAppCompatVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "androidx.vectordrawable:vectordrawable:$rootProject.androidxVectorDrawableVersion"
    implementation "androidx.preference:preference-ktx:$rootProject.androidxPreferenceVersion"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            Production(MavenPublication) {
//                from components.release
                groupId = 'com.urbanairship.android'
                artifactId = project.name
                version = project.version.toString()

                artifact("$buildDir/outputs/aar/${project.name}-release.aar")
                artifact(sourcesJar)

                pom {
                    name.set(project.name)
                    description.set(project.description)
                    url.set("https://github.com/urbanairship/android-library")

                    developers {
                        developer { name.set("Airship") }
                    }

                    licenses {
                        license {
                            name.set("The Apache Software License, Version 2.0")
                            url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                            distribution.set("repo")
                        }
                    }

                    scm {
                        connection.set("https://github.com/urbanairship/android-library.git")
                        url.set("https://github.com/urbanairship/android-library")
                    }
                }

                pom.withXml {
                    def dependencies = asNode().appendNode("dependencies")
                    configurations["implementation"].allDependencies.forEach {
                        def dep = dependencies.appendNode("dependency")
                        dep.appendNode("groupId", it.group)
                        dep.appendNode("artifactId", it.name)
                        dep.appendNode("version", it.version)
                    }
                }
            }
        }
    }
}